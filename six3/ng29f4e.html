<HTML>
<HEAD><TITLE>SIx Driver RDD v3.00 - Reference Guide - Long Entry</TITLE></HEAD>
<BODY>

<A HREF="ng29a9e.html">[&lt;&lt;Previous Entry]</A>
<A HREF="ng435.html">[^^Up^^]</A>
<A HREF="ng2a5f3.html">[Next Entry&gt;&gt;]</A>
<A HREF="menu.html" >[Menu]</A>
<A HREF="info.html">[About The Guide]</A>
<HR>
<PRE>

  <B>Sx_IDtype():</B>

  Syntax:   Sx_IDtype()

  Returns:  Numeric value indicating the type of the current record or
            identity.

                0 - No Identity
                1 - Standard Record
                2 - Encrypted Record

  Description:  This function allows you to determine the type of the
                current identity.  The term "identity" was introduced in
                CA-Clipper 5.2 to reference a record without necessarily
                using a fixed record number.  This provides a means to
                identify a specific record even if it's record number is
                subject to change (as record numbers can do under Paradox).

                At the present time, this function is used primarily as a
                means to detect if the current identity had been previously
                encrypted or not.

  Example:

    // Assume TEST.DBF has 10 records in it already

    LOCAL nType := 0, nCount := 0

    USE test VIA "SIXCDX"         // Open TEST.DBF
    Sx_SetPass( "HOMER" )         // Select a password value
    Sx_DBFencrypt()               // Encrypt all 10 records in TEST.DBF

    Sx_SetPass("")                // Clear the password value
    FOR nCount := 1 To 10         // Now append 10 new records into
      dbAppend()                  //  the database and store some text
      test-&gt;LAST := "Testing"     //  into the LAST field
    NEXT

    dbGoTop()                     // Go back to the top of the database
    DO WHILE !eof()               //  and skip through each record to EOF

      ? RecNo()
      nType := <B>Sx_IDtype()</B>        // Get this "identity's" type
      DO CASE
        CASE nType == 0
          ?? ": No Identity!"
        CASE nType == 1
          ?? ": Unencrypted"      // Records 11 thru 20 should display this
        CASE nType == 2
          ?? ": Encrypted"        // Records 1 thru 10 should display this
      ENDCASE
      dbSkip()                    // Move to next record

    ENDDO

</PRE>
<HR>
<B>See Also:</B>
<A HREF="ng3ba0f.html">Sx_TableType()</A>
<A HREF="ng23407.html">Sx_DBFencrypt()</A>
<A HREF="ng22e80.html">Sx_DBFdecrypt()</A>
<HR>
This page created by ng2html v1.05, the Norton guide to HTML conversion utility.
Written by <A HREF="http://www.acemake.com/hagbard">Dave Pearson</A>
<HR>
</BODY>
</HTML>
