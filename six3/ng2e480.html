<HTML>
<HEAD><TITLE>SIx Driver RDD v3.00 - Reference Guide - Long Entry</TITLE></HEAD>
<BODY>

<A HREF="ng2d4d1.html">[&lt;&lt;Previous Entry]</A>
<A HREF="ng435.html">[^^Up^^]</A>
<A HREF="ng2eff1.html">[Next Entry&gt;&gt;]</A>
<A HREF="menu.html" >[Menu]</A>
<A HREF="info.html">[About The Guide]</A>
<HR>
<PRE>

  <B>Sx_KeyCount():</B>

  Syntax:  Sx_KeyCount( [nOrder] | [cTagName [, cIndexName ]] [, lLockIndex] )

           nOrder     = Position of tag in list of tag orders.
           cTagName   = Name of tag.
           cIndexName = Name of index file which contains cTagName.
           lLockIndex = Logical value indicating index locking mode to be
                        used.  The default is .T. (no index locking).

  Returns:   &lt;numeric&gt; Number of index keys in the current or specified
                  index file or tag.

  Description:    This function counts the number of keys in the current
                  or specified index or tag and returns that value.  If
                  the current or specified index is a compound index then
                  the currently active tag is used.

                  This function respects any active index SCOPE and will
                  return the number of keys within the scope condition.  It
                  does <B>not</B>, however, respect a FILTER condition.  In that
                  scenario, it's best to use a COUNT command with Mach SIx.

    <B>NOTE:  Keep in mind that this function determines its return value</B>
    <B>       strictly based on KEYS in the index file.  If you have marked</B>
    <B>       records in your database for deletion, the index KEY for those</B>
    <B>       records will STILL be in the index, and will STILL be counted.</B>
    <B>       If you do not want Sx_KeyNo() or Sx_KeyCount() to count keys</B>
    <B>       for records marked as deleted, you must create the index as a</B>
    <B>       CONDITIONAL index FOR !DELETED()</B><B>.  For example:</B>

    <B>           INDEX ON last TAG last FOR !Deleted()</B>

    NOTE:  This function is NOT supported under SIXNTX.

  Example:

    #include "SIXCDX.CH"

    // Create a standard Roll-Your-Own (RYO) Index

    USE test VIA "SIXCDX"               // Open database
    INDEX ON last TO last EMPTY         // Create EMPTY index file
    GOTO 10                             // Select a record
    Sx_KeyAdd()                         // Add a key for this record
    ? Sx_KeyData()                      // Displays key value for this rec
    ? <B>Sx_KeyCount()</B>                     // 1
    Sx_KeyDrop()                        // Delete this key from the index
    ? <B>Sx_KeyCount()</B>                     // 0

    // Create a Multi-Keyed, Free-Format RYO Index

    INDEX ON sxChar(12) TO phones       // Create empty index file
    GOTO 100                            // Select a record
    ? <B>Sx_KeyCount()</B>                     // 0
    Sx_KeyAdd( 0,0,"213-555-1212" )     // Add one phone number for this rec
    Sx_KeyAdd( 0,0,"213-555-9999" )     // Add another to the same record
    Sx_KeyAdd( 0,0,"310-222-9876" )     // And yet another phone number!
    ? <B>Sx_KeyCount()</B>                     // 3

    // Now delete all phone numbers (keys) for this record
    IF Sx_FindRec( 100, .F. )           // Find first key for this record
      ? "Deleting: " + Sx_KeyData()     // Display key value being deleted
      Sx_KeyDrop()                      // Delete first phone number
      DO WHILE Sx_FindRec( 100, .T. )   // Find any other keys for this rec
        ? "Deleting: " + Sx_KeyData()   // Display key value being deleted
        Sx_KeyDrop()                    // Delete other phone numbers
      ENDDO
    ENDIF
    ? <B>Sx_KeyCount()</B>                     // 0


</PRE>
<HR>
<B>See Also:</B>
<A HREF="ng306aa.html">Sx_KeyNo()</A>
<A HREF="ng80ce.html">hs_KeyCount()</A>
<A HREF="ng2d4d1.html">Sx_KeyAdd()</A>
<A HREF="ng2f75e.html">Sx_KeyDrop()</A>
<A HREF="ng2eff1.html">Sx_KeyData()</A>
<A HREF="ng2a5f3.html">Sx_IndexCount()</A>
<HR>
This page created by ng2html v1.05, the Norton guide to HTML conversion utility.
Written by <A HREF="http://www.acemake.com/hagbard">Dave Pearson</A>
<HR>
</BODY>
</HTML>
