<HTML>
<HEAD><TITLE>SIx Driver RDD v3.00 - Reference Guide - Long Entry</TITLE></HEAD>
<BODY>

<A HREF="ng386d6.html">[&lt;&lt;Previous Entry]</A>
<A HREF="ng435.html">[^^Up^^]</A>
<A HREF="ng39ced.html">[Next Entry&gt;&gt;]</A>
<A HREF="menu.html" >[Menu]</A>
<A HREF="info.html">[About The Guide]</A>
<HR>
<PRE>

  <B>Sx_SetTurbo():</B>

  Syntax:   Sx_SetTurbo([lTurboRead])

            lTurboRead = Logical value indicating whether or not to
                         allow "dirty" index reads

  Returns:  Logical value indicating the current "dirty read" setting.

  Description:   This function sets/returns the "dirty read" setting for
                 the SIx Driver.  Setting "dirty read" to .T. allows
                 your application to read through index locks on a shared
                 index.

                 <B>CAUTION: This is an advanced feature and should be used</B>
                 <B>with caution!</B>  If dirty reads are done on an index that
                 is updated by other users, then there is no guarantee
                 that the index will be correctly positioned.

                 This function is used by the following commands:

                     <B>SET TURBOREAD ON</B>
                     <B>SET TURBOREAD OFF</B>

    NOTE:  This function is NOT supported under SIXNTX.

  Example:

    /*
        This program demonstrates the speed difference TURBOREAD makes when
        skipping though a shared database.
    */

    #include "SIXCDX.CH"

    LOCAL nStart, nEnd

    SET EXCLUSIVE OFF         // Make shared the default

    USE TEST VIA "SIXCDX"     // Open up the TEST database
    INDEX ON last TO last     // Build an index; Since it's exclusive after
    CLOSE DATA                //  just creating it, close the database and
    USE TEST VIA "SIXCDX"     //  reopen it
    SET INDEX TO LAST         // Then set our index active again

    ? "Skip test - shared"
    ?
    nStart := Seconds()            // Save starting time
    FOR nCnt := 1 TO 240           // Cruise through the database for a bit
      ?? "."                       // Print a dot
      DO WHILE !eof()              // Skip to the end of the file
        SKIP
      ENDDO
      DO WHILE !bof()              // Skip back to the beginning of the file
        SKIP -1
      ENDDO
    NEXT
    nEnd := Seconds()              // Save ending time

    ? "Elapsed time:", nEnd - nStart, "seconds"
    ?

    // Now turn dirty read on
    <B>Sx_SetTurbo(.T.)</B>

    ? "Skip test - shared with dirty read on"
    ?
    nStart := Seconds()            // Save starting time
    FOR nCnt := 1 TO 240           // Cruise through the database for a bit
      ?? "!"                       // Print an exclamation point
      DO WHILE !eof()              // Skip to the end of the file
        SKIP
      ENDDO
      DO WHILE !bof()              // Skip back to the beginning of the file
        SKIP -1
      ENDDO
    NEXT
    nEnd := Seconds()              // Save ending time

    ? "Elapsed time:", nEnd - nStart, "seconds"


</PRE>
<HR>
<B>See Also:</B>
<A HREF="ng51ea9.html">SET TURBOREAD</A>
<A HREF="ng80905.html">What is TURBOREAD?</A>
<HR>
This page created by ng2html v1.05, the Norton guide to HTML conversion utility.
Written by <A HREF="http://www.acemake.com/hagbard">Dave Pearson</A>
<HR>
</BODY>
</HTML>
