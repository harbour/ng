<HTML>
<HEAD><TITLE>SIx Driver RDD v3.00 - Reference Guide - Long Entry</TITLE></HEAD>
<BODY>

<A HREF="ng43493.html">[&lt;&lt;Previous Entry]</A>
<A HREF="ng40c12.html">[^^Up^^]</A>
<A HREF="ng455ad.html">[Next Entry&gt;&gt;]</A>
<A HREF="menu.html" >[Menu]</A>
<A HREF="info.html">[About The Guide]</A>
<HR>
<PRE>

  <B>COUNT:</B>

  Syntax:

  <B>COUNT TO &lt;idVar&gt; [&lt;scope&gt;] [WHILE &lt;lCondition&gt;]</B>
   <B>        [FOR &lt;lCondition&gt;] [NOOPTIMIZE]</B>

  <B>TO &lt;idVar&gt;</B> identifies the variable to assign the COUNT result.  A
  variable that either does not exist or is not visible is created as a
  private variable whose scope is the current procedure.

  <B>&lt;scope&gt;</B> is the portion of the current database file to COUNT.  The
  default is ALL records.  Mach SIx only supports the ALL scope.

  <B>WHILE &lt;lCondition&gt;</B> specifies the set of records meeting the condition
  from the current record until the condition fails.  Mach SIx does not
  support the WHILE condition.

  <B>FOR &lt;lCondition&gt;</B> specifies the conditional set of records to COUNT
  within the given scope.  Mach SIx will optimize any FOR condition that
  contains one or more active index keys.

  <B>NOOPTIMIZE</B>, preempts optimization and forces default Clipper processing.

  <B>Description: </B>

  COUNT tallies the number of records from the current work area that match
  the specified record scope and conditions.  The result is then placed in
  the specified variable.  &lt;idVar&gt; can be a variable of any storage class
  including a field.

  The COUNT command may be optimized by specifying one or more active index
  keys in the FOR condition.  Note that Mach SIx will only optimize a COUNT
  with a FOR condition and a scope of ALL.  WHILE conditions and alternate
  scopes cannot be optimized by Mach SIx.

  <B>Performance Tip: </B>

  With SET DELETED OFF, fully optimized counts can be calculated directly
  from the active index files (<B>no database access is required</B>), and the
  fastest count times will be achieved.

  <B>---------------------------------| EXAMPLE |--------------------------------</B>
  /*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
  *  Demonstrates an optimized COUNT command                                *
  *::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*/

  #include "SIXCDX.CH"
  #include "MACHSIX.CH"

  LOCAL nCount := 0

  USE Account VIA "SIXCDX"  // Structural index ACCOUNT.CDX opened
                            // automatically
  CLEAR SCREEN
  ? "Example of optimized COUNT command."
  ?

  // Fully optimized count of records, taking advantage of the STATE tag
  <B>COUNT TO nCount FOR account-&gt;STATE = "CA" .or. State = "FL"</B>

  // Fully optimized count making use of multiple index tags
  <B>COUNT TO nCount FOR (Age &gt;= 20 .AND. Age &lt; 30) .AND. ;</B>
  <B>                State = "AK" .and. City = "Sicily"</B>

  // Partially optimized count of records for Orlando residents with an
  // income over 50,000 dollars.
  <B>COUNT TO nCount FOR City = "Orlando" .AND. Income &gt; 50000</B>
  CLOSE ALL

</PRE>
<HR>
This page created by ng2html v1.05, the Norton guide to HTML conversion utility.
Written by <A HREF="http://www.acemake.com/hagbard">Dave Pearson</A>
<HR>
</BODY>
</HTML>
