<HTML>
<HEAD><TITLE>SIx Driver RDD v3.00 - Reference Guide - Long Entry</TITLE></HEAD>
<BODY>

<A HREF="ngf1e4.html">[&lt;&lt;Previous Entry]</A>
<A HREF="ng435.html">[^^Up^^]</A>
<A HREF="ng1025b.html">[Next Entry&gt;&gt;]</A>
<A HREF="menu.html" >[Menu]</A>
<A HREF="info.html">[About The Guide]</A>
<HR>
<PRE>

  <B>m6_FiltInfo():</B>

  Purpose:  Get information about a specified filter.  Filter may be
            a system or RYO filter, all you need is a valid handle to
            the filter.

   Syntax:  m6_FiltInfo( &lt;nHandle&gt; )

    Where:  &lt;nHandle&gt;      --  Is any valid handle to a filter.

  Returns:  A single dimensional array containing information about the
            specified filter.

     Note:  Use the following constants defined in MachSIx.ch to reference
            the elements of the info array.

            #define INFO_EXPR         1  // Complete filter expression
            #define INFO_NONEXPR      2  // Non-indexed expression
            #define INFO_OPTLVL       3  // Optimization level
            #define INFO_COUNT        4  // Number of records in filter
            #define INFO_SIZE         5  // Maximum valid record no.
            #define INFO_OWNER        6  // Filter Owner, 1=System, 2=User
            #define INFO_POS          7  // Current position in filter

            *Always free RYO filters that are no longer being used.*
  Example:

         #include "SIXCDX.CH"
         #include "MACHSIX.CH"

         USE Customer via "SIXCDX"

         SET INDEX TO State

         //...............create a RYO filter based on a query condition
         nHandle := m6_NewFilter( 'State = "CA" .and. Age = 25' )

         //.............................get information about the filter
         aInfo := <B>m6_FiltInfo( nHandle )</B>

         //.......................................display filter details
         ? "Filter information"
         ? "------------------"
         ? aInfo[ INFO_EXPR    ]      //  STATE="CA".AND.AGE=25
         ? aInfo[ INFO_NONEXPR ]      //  AGE=25
         ? aInfo[ INFO_OPTLVL  ]      //  1
         ? aInfo[ INFO_COUNT   ]      //  100
         ? aInfo[ INFO_SIZE    ]      //  10000
         ? aInfo[ INFO_OWNER   ]      //  2
         ? aInfo[ INFO_POS     ]      //  1

         //...........................free memory allocated to RYO filter
         m6_FreeFilter( nHandle )

         //.........................................set a workarea filter
         SET FILTER TO "State='CA'"

         //.........................................initialize the filter
         GO TOP

         //....get a handle to the filter ( note: it's still system owned )
         nHandle := m6_GetAreaFilter()

         //.............................get information about the filter
         aInfo := <B>m6_FiltInfo( nHandle )</B>

         //.......................................display filter details
         ? "Filter information"
         ? "------------------"
         ? aInfo[ INFO_EXPR    ]      //  STATE='CA'
         ? aInfo[ INFO_NONEXPR ]      //  ""
         ? aInfo[ INFO_OPTLVL  ]      //  2
         ? aInfo[ INFO_COUNT   ]      //  100
         ? aInfo[ INFO_SIZE    ]      //  10000
         ? aInfo[ INFO_OWNER   ]      //  1
         ? aInfo[ INFO_POS     ]      //  1


</PRE>
<HR>
<B>See Also:</B>
<A HREF="ng159ed.html">m6_NewFilter()</A>
<A HREF="ng136a1.html">m6_FreeFilter()</A>
<A HREF="ng13b9a.html">m6_GetAreaFilter()</A>
<A HREF="ngce09.html">m6_Error()</A>
<HR>
This page created by ng2html v1.05, the Norton guide to HTML conversion utility.
Written by <A HREF="http://www.acemake.com/hagbard">Dave Pearson</A>
<HR>
</BODY>
</HTML>
